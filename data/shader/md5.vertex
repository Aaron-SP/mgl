#version 300 es

layout (location = 0) in vec4 vertex;
layout (location = 1) in vec2 uv;
layout (location = 2) in vec3 normal;
layout (location = 5) in vec4 bone_index;
layout (location = 6) in vec4 bone_weight;

#define MAX_NUM_TOTAL_MATRIX 100
layout(std140) uniform matrix_block
{
    mat4 matrix[MAX_NUM_TOTAL_MATRIX];
    int matrix_size;
};

#define MAX_NUM_TOTAL_VECTOR 1
layout(std140) uniform vector_block
{
    vec4 vector[MAX_NUM_TOTAL_VECTOR];
    int vector_size;
};

out vec4 out_cam_position;
out vec3 out_normal;
out vec2 out_uv;
out vec4 out_vertex;

void main(void)
{
    // Get the model matrix from the uniform buffer
    mat4 pv = matrix[0];
    mat4 model = matrix[1];

    // Get the camera position
    out_cam_position = vector[0];

    // Perform vertex skinning
    mat4 bone_transform = matrix[int(bone_index.x) + 2] * bone_weight.x;
	bone_transform += matrix[int(bone_index.y) + 2] * bone_weight.y;
	bone_transform += matrix[int(bone_index.z) + 2] * bone_weight.z;
	bone_transform += matrix[int(bone_index.w) + 2] * bone_weight.w;

    // Calculate the vertex in world coordinates
    out_vertex = model * bone_transform * vertex;

    // Calculate the normal in world coordinates
    out_normal = normalize(mat3(model) * normal);

	// Calculate transformed position
	gl_Position =  pv * out_vertex;

	// Pass texture coordinates to fragment shader
	out_uv = uv;
}
